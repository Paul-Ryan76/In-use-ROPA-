# SparQl Query for Article 30 extract 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dpv: <https://w3id.org/dpv/dpv-skos#>
PREFIX dpv-gdpr: <https://w3id.org/dpv/dpv-skos/dpv-gdpr#>
PREFIX dpv-pd: <https://w3id.org/dpv/dpv-skos/dpv-pd#>
PREFIX dpcat: <https://w3id.org/dpcat#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX time: <https://www.w3.org/TR/owl-time/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX uplison: <https://w3id.org/dpcat/examples/UNIPHAR/vocab#>

SELECT DISTINCT ?Entry ?title ?purpose ?datasubject ?personaldata 
    ?recipient ?legalbasis ?transfer_location
WHERE {
    ?Entry a dpcat:ROPARecord .
    ?Entry dct:title ?title .
    ?Entry dpv:hasPurpose/skos:prefLabel ?purpose .
    ?Entry dpv:hasDataSubject/skos:prefLabel ?datasubject .
    ?Entry dpv:hasPersonalData/skos:prefLabel ?personaldata .
    OPTIONAL { ?Entry dpv:hasRecipient/dpv:hasName ?recipient } .
    OPTIONAL { ?Entry dpv:hasLegalBasis/skos:prefLabel ?legalbasis . }
    OPTIONAL { ?Entry dpv:hasProcessing ?processing . 
        ?processing a dpv:Transfer . 
        ?processing dpv:hasLocation/skos:prefLabel ?transfer_location } } 
